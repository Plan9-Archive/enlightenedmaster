!% -v8
!% -SDXes
!% $MAX_VERBS=165
!% $MAX_LABELS=1100

! Library Version: Inform 6 -- Release 6/12-beta1 -- Serial number 140724

!============================================================================
! Configuration constants

Release 3;
Serial "170402";
Constant Story "Enlightened Master^";
Constant AMUSING_PROVIDED;
Constant DEATH_MENTION_UNDO;
Constant MAX_SCORE = 32000;
Constant TASKS_PROVIDED;
Constant NUMBER_TASKS = 63;
Array task_scores -> 
	9 9 9 9 9 9 9 9 9 
	9 9 9 9 9 9 9 9 9 
	9 99 9 9 9 9 9 9 9 
	9 9 9 9 9 9 9 9 9 
	9 9 9 9 9 9 69 9 9 
	9 39 9 99 9 9 9 9 9
	9 9 9 9 9 99 99 9 9;

Constant Headline
"An interactive transcendence^
By Ben Kidwell and Maevele Straw^";

!============================================================================
! Include parser, new library messages, verblib

Include "Parser";
Object  LibraryMessages
	with	
	before [;
	Attack:	"That isn't your approach.";
	Buy:    "Nothing is bought or sold here.";
	Climb:	<<go u_obj>>;
	Dig:    "The ground is frozen here.";
	Drink:	"You aren't thirsty.";
	Fill:	"That doesn't work.";
	Jump:	"You hop and accomplish nothing.";
	Kiss:	"There are probably better ways to express your feelings of love and devotion.";
	Listen:	"You hear the wind.";
	Mild:	"Okay.";
	Pray:   "You meditate.";
	Sing:   "You chant a mantra of compassion.";
	Smell:	"You smell nothing special, but life itself has a slight scent of lavender and nag champa.";
	Sorry:  "Rather than being caught up in apologies and regrets, you try to maintain focus on future improvement.";
	Strong: "Your words are unheeded by the mountain air.";
	Swim:   "Can't swim here.";
	Swing:	"Nothing to swing from.";
	Taste:	"Everything tastes delicious, with a slight undertone of ginger and fresh basil.";
	Think:	goalssub();
			return true;
	Wait:	"You wait calmly.";
	Wake:	"You are already alert.";
	Miscellany:
		if (lm_n == 38)
			"Not possible.";
		if (lm_n == 39)
			"This game world does not provide that level of detail.";
	];
Include "verblib";

!============================================================================
! Global variables

Global clockoff = false;
Global roll = 0;
Global x = 0;
Attribute shootable;
Attribute gametarget;

!============================================================================
! Object classes

Class	Room
	with
	wintext 0,
	description "UNIMPLEMENTED ROOM DESCRIPTION",
	has	light;

Class	Scene
	with
	wintext 0,
	description "UNIMPLEMENTED SCENE DESCRIPTION",
	before [;
		drop: 
			"Mulitverse travel protip: do NOT drop your items while visiting alternate universes.";
	],
	cant_go "Your presence in this branch of the multiverse is anchored to this scene, you must participate before you can leave.",
	has light;

Class	Decor
	with
	description "UNIMPLEMENTED DECOR DESCRIPTION",
	has	scenery;

Class	Target
	with
	description [;
		if(self hasnt shootable)
			print_ret "The ", (name) self, " is not currently shootable.^";
		x = 100 - self.difficulty + skill.shoot;
		if (x > 80) x = 80;
		x = x + skill.luck + (skill.nudge * skill.nudgeflag);
		if (x < 0) x = 0;
		if (x > 100) x = 100;
		print "You have a ", x, "% chance of hitting the ", (name)self, " and a ";
		x = self.risk - skill.shoot;
		if (x < 10) x = 10;
		x = x - skill.luck - (skill.nudge * skill.nudgeflag);
		if (x < 0) x = 0;
		print x, "% chance of losing your ball.^";
	],
	hitcount 0,
	value 25,
	risk 50,
	difficulty 100,
	hit [;
		self.hitcount++;
		em.points = em.points + self.value;
		print_ret "Hit number ", self.hitcount, " on ", (name)self, " +", self.value, " points^";
	],
	triggers [;
	],
	newgame [;
	],
	has gametarget,
	has scenery,
	has shootable;

Class	Prop
	with
	description "UNIMPLEMENTED PROP DESCRIPTION",
	has	static;

!Class	Character
!	with
!	description "UNIMPLEMENTED ACTOR DESCRIPTION",
!	has	proper animate;

!===========================================================================
! Game rooms, characters, props and objects, storyline and verbs

Include "emroom.inf";

Include "emobj.inf";

Include "emstor.inf";

Include "emverb.inf";

