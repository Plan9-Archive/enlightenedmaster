!============================================================================
! The game locations

Room	base "At the base of an immense mountain"
	with
	name 'base' 'mountain',
	description "A huge mountain towers above you, a vertical landscape of grey rock and glittering snow.",
	u_to icefall,
	cant_go "Your path is upward.";

Room	icefall "In the midst of a treacherous icefall"
	with
	description "You are in the midst of a potentially lethal maze of giant slabs and boulders of ice. A frozen river trapped in a crystallized waterfall. You hear ominous creaks and snaps from the gradual shifting of the ice around you.",
	d_to base,
	u_to camp_low;

Room	camp_low "Campsite, Low Altitude"
	with
	camped 0,
	description "A sheltered overhang provides space for the lowest campsite on the flanks of the mountain.",
	before [;
		camp:	self.camped = 1;
				"You set up your tent and crawl inside. Despite your fatigue, it takes you awhile to fall asleep, as the anticipation is electric within you.";
	],
	u_to [;
		if (self.camped == 0)
			"You need to CAMP and continue up the mountain tomorrow.";
		return snow_hill;
	],
	d_to icefall;

Room	snow_hill "Long sloped snowfield"
	with
	description "A seemingly endless expanse of snow angles upward ahead of you. The scale is vast and overwhelming, and the frigid air makes the bigness almost painfully isolating.",
	d_to camp_low,
	u_to boulders;

Room	boulders "Piled boulders"
	with
	description "The angle of the mountain becomes abruptly steeper, and the transition is marked by a wide band of piled boulders, evidently at rest here after breaking off the mountain's higher altitudes. The footing is treacherous and the pace slow.",
	d_to snow_hill,
	u_to middle_wall;

Room	middle_wall "Near-vertical wall"
	with
	description "The first section of technical rock climbing is a five hundred foot slab of nearly vertical rock. With slow and careful use of safety ropes and anchor points, you have reached the midpoint of the cliff section.",
	d_to boulders,
	u_to camp_mid;

Room	camp_mid "Campsite, Middle Altitude"
	with
	camped 0,
	description "A broad shelf of rock above the cliff is a suitable site for the mid-level campsite.",
	before [;
		camp:	self.camped = 1;
				"You set up your tent and crawl inside. You eat a cup of cous-cous and try to keep your mind clear despite your cold and aching body.";
	],
	u_to [;
		if (self.camped == 0)
			"You need to CAMP and continue up the mountain tomorrow.";
		return exposed_ridge;
	],
	d_to middle_wall;

Room	exposed_ridge "Exposed ridge"
	with
	description "The path upward is a knife-edged ridge that ascends gradually toward the summit, with precipitous descents on either side. The howling wind sometimes forces you to crouch and attempt to anchor yourself with your ice axe.",
	d_to camp_mid,
	u_to ice_chimney;

Room	ice_chimney "Narrow ice-filled rock chimney"
	with
	description "The climb becomes grim and precarious as it reaches another vertical section, this one composed of a series of ice-filled creases pointing toward the peak. You probe the ice with screws, seeking secure anchor, and try to control your labored breathing in the thin air.",
	d_to exposed_ridge,
	u_to traverse;

Room	traverse "Horizontal traverse"
	with
	description "Upward momentum is interrupted by a menacing overhanging shelf of ice. It is necessary to traverse sideways beneath it. You try to balance safety with speed, knowing that at any moment, ice falling from above could tear you off the side of the mountain.",
	d_to ice_chimney,
	u_to camp_high;

Room	camp_high "Campsite, High Altitude"
	with
	camped 0,
	description "There is no comfort here save a patch of snow just wide enough to pitch a tent. The wind howls and moans ceaselessly. The view, however, is staggering. The other mountains in the range, tall as they are, are now far below you.",
	before [;
		camp:	self.camped = 1;
				"You set up your tent and crawl inside. Tomorrow, you think as you sink into a restless, low-oxygen sleep, tomorrow is the day.";
	],
	u_to [;
		if (self.camped == 0)
			"You need to CAMP and continue up the mountain tomorrow.";
		return high_wall;
	],
	d_to traverse;

Room	high_wall "High wall, summit approach"
	with
	description "The final vertical wall section is even taller and more featureless, almost abstract in its completely impersonal and unyielding solidity. Your tools and gear are almost useless here, and you feel sure that your few precarious placements would not save you if you lost your grip.",
	d_to camp_high,
	u_to final_approach;

Room	final_approach "Gale-swept crags, summit approach"
	with
	description "Near the summit, the wind attains a malevolent ferocity. The sharp crags and outcroppings of the final ascent act as funnels, forcing you to detour around gaps from which the wind emerges with the force of a cannon. The cold has penetrated so deeply that numbness has spread from your limbs into your torso.",
	d_to high_wall,
	u_to summit;

Room	summit	"Summit, by cave entrance"
	with
	description "You have attained the summit, but you feel no surge of triumph. The physical peak means nothing to you. To reach the threshold of this cave, you would have as happily plumbed the ocean, trekked deserts, or searched the city slums. The summit is a bleak pinnacle of frozen granite with a panoramic view of the surrounding mountains. The entrance to the cave of the Enlightened Master is a few meteres below the summit, a jagged darkness angling back down into the mountain.",
	d_to final_approach,
	in_to cave_entrance;

Room	cave_entrance "Cave entrance"
	with
	description "You have finally reached your goal, the cave where the Enlightened Master is to be found. A path is visible leading downward.",
	out_to summit,
	d_to master_threshold;

Room	master_threshold "Shadowy cavern"
	with
	description "The light grows dim as the path twists away from the entrance. After a few turns, the path enters a larger space. All that is visible is blue-black shadow, save for the western wall, which flickers dimly. You hear a faint sound of bells from the west.",
	u_to cave_entrance,
	w_to master_chamber;

Room	master_chamber "Chamber of the Enlightened Master"
	with
	description "Here, deep within the cave atop the Highest Mountain, you have found the Enlightened Master.^
^
It's a pinball machine.",
	e_to master_threshold;

Scene	orchard "Golden Apple Orchard"
	with
	name 'orchard',
	turn 0,
	description "This is the orchard where the Golden Apples grow. The trees have a wild diversity of shapes and sizes, but each bears shiny metallic fruit.",
	daemon [;
		self.turn++;
		switch(self.turn){
		1: "^~I wanted you to see this place, I spent a lot of time here when I was growing up.~";
		2: "^~Lazy summer afternoons playing tag with friends, lots of running in circles around the trees and occasionally into them.~";
		3: "^~Anyway, I just wanted you to see this. Why don't you take an apple as a souvenir?~";
		default: "^~Go ahead, TAKE APPLE.~";
		}
	];

Scene	highway "Somewhere on the Highway, driving alone"
	with
	name 'highway',
	travel 0,
	turn 0,
	description "~I was driving at random, tired of the small town I had been working in, taking myself to a different small town. I felt like a boat floating down a black river of highway, watched over by the moon.~",
	daemon [;
		self.turn++;
		if(self.turn > 10) {
			playerto(master_chamber);
			stopdaemon(self);
			"~Thanks for sharing that memory with me, time to get back to the game!~";
		}
		if(self.turn > 2 && self.travel == 0)
			"All you can do is travel whatever direction you wish.";
	],
	cant_go [;
		self.travel++;
		switch(self.travel) {
		1: "~I remember the way the forest sometimes felt like a comforting blanket when it wrapped itself around the road.~";
		2: "~Those hilly back-roads, nothing to see except trees and an occasional mailbox.~";
		3: "~Rolling slowly through a small town, quiet and dark except for the neon glow of a beer sign in a corner tavern.~";
		4: "~Sometimes, the distant sound of a train whistle floating on the night air.~";
		5: "~Once that feeling got deep enough inside me, it just stayed, even in the midst of hectic shifts at work.~";
		6: "~Farmhouses in the darkness, cornfields, and more cornfields.~";
		7: "~Up onto a larger highway, smoother, faster, abstract correspondence of distance and time.~";
		8: "~Still looking for the next Right Place to be, to hear more stories, to understand the big picture by fading into its background.~";
		9: "~Across the big river, lattice of steel beams strobing by in the headlights, the smell of moving water.~";
		10: "~Silhouettes of crows on a telephone wire, watching the intersection as you pass through.~";
		11: "~Slowing the car to a crawl on an empty stretch of road, rolling the windows all the way down to smell the air and hear the buzz of the crickets.~";
		}
	];

Scene	festival "At a music festival, about to go onstage"
	with
	name 'festival',
	turn 0,
	description "~I was at the Michigan Witch-Again pagan music festival, doing my magic-and-philosophy act opening for Climbing the Waltz.~",
	daemon [;
		self.turn++;
		if(self.turn > 10) {
			playerto(master_chamber);
			stopdaemon(self);
			"~Thanks for sharing that memory with me, time to get back to the game!~";
		}
		switch(self.turn) {
		1: "~Outdoor shows at summer festivals were the best part of my time as a stage performer.~";
		2: "~I'd wander through the parking lot and camping areas beforehand, juggling a pair of apples with my right hand and making a coin vanish and reappear with my left.~";
		3: "~I had a pretty good line of patter I'd deliver as I was wandering. 'In an hour on stage, you will see reality taken apart and put back together through the power of Logic.'~";
		4: "~'Learn that the infinity of stars in the sky is a tiny number and to change your universe with forcing extensions.'~";
		5: "~'You will dance along the edge of the cliffs of paradox and see the full length of the mirror-tunnel of recursion.'~";
		}
	];

