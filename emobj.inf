!============================================================================
! The room decorations

Decor	instructions "instructions" master_chamber
	with
	name 'card' 'instruction' 'instructions',
	description "PLAY to start game. Control with targeted commands, such as SHOOT LEFT RAMP, SHOOT RIGHT RAMP, SHOOT BUMPERS. You can also try FLIP to play a full ball automatically.";

Target	lramp "left ramp" master_chamber
	with
	name 'left' 'ramp',
	triggers [;
		if((choicelock.lockready == 0) && (rramp.hitcount > 0)){
			choicelock.lockready = 1;
			"~Good job, the Infinity ramp has activated Axiom of Choice lock. That's a hard shot though.~";
		}
	];

Target	rramp "right ramp" master_chamber
	with
	name 'right' 'ramp',
	triggers [;
		if((choicelock.lockready == 0) && (lramp.hitcount > 0)){
			choicelock.lockready = 1;
			"~Nicely played, the Infinity ramp has activated Axiom of Choice lock. That one's tricky so be careful.~";
		}
	];

Target	magnet "forcing magnet" master_chamber
	with
	name 'forcing' 'magnet',
	value 50,
	risk 65,
	difficulty 125,
	level 0,
	triggers [;
		if(self.level > 0)
			return true;
		if(choicelock.multicount > 0 && bumpers.hitcount > 49) {
			em.points = em.points + 1000;
			self.level++;
			"~Wow! You got Induction on N, Axiom of Choice Multiball, AND you've hit the Forcing Magnet. That's a thousand point bonus for you.~";
		}
	];

Target	bumpers "bumpers" master_chamber
	with
	name 'bumper' 'bumpers' 'successor',
	value 2,
	risk 40,
	difficulty 75,
	bounces 0,
	goal 50,
	newgame [;
		self.value = 2;
		self.goal = 50;
	],
	hit [;
		self.bounces = random(10);
		self.hitcount = self.hitcount + self.bounces;
		em.points = em.points + (self.bounces * self.value);
		print "The ball gets ", self.bounces, " hits in the successor bumpers for ", self.bounces * self.value, " points.^";
		if(self.hitcount < self.goal)
			print self.goal - self.hitcount, " more hits to reach Induction on N.^";
	],		
	triggers [;
		if(self.value == 2 && self.hitcount > 20){
			self.value = 4;
			"~Since you like those bumpers so much, I'm doubling their value for you.~";
		}
		if(self.value == 4 && self.hitcount > 49){
			self.value = 9;
			"Successor function reaches Induction on N. Opening Ordinal Staircase. (UNIMPLEMENTED)";
		}
	];

Target	choicelock "choice lock" master_chamber
	with
	name 'axiom' 'choice' 'lock' 'aoc',
	value 75,
	risk 75,
	difficulty 150,
	lockready 0,
	hasball 0,
	multicount 0,
	newgame [;
		self.lockready = 0;
		self.hasball = 0;
	],
	triggers [;
		if(self.lockready == 0)
			"You need to activate both sides of the Infinity ramp to prime the Axiom lock.";
		if(self.hasball == 1){
			self.hasball = 0;
			self.multicount++;
			em.points = em.points + 500;
			"MULTIBALL! 500 point bonus.";
		}
		self.hasball++;
		"The ball hits the Axiom of Choice lock dead-on and the mechanism grabs it with a satisfying thunk.^";
	];

!============================================================================
! The immobile prop objects

Prop	em "Enlightened Master" master_chamber
	with
	article "the",
	name 'enlightened' 'master' 'pinball' 'machine' 'game' 'table' 'pin',
	points 0,
	hiscore 0,
	ingame 0,
	balls 0,
	gamenumber 0,
	description [;
		print_ret "The Enlightened Master is a pinball machine with a purple cabinet and gold metal. The standard flipper buttons are bright pink. A card in the corner of the machine is labelled 'INSTRUCTIONS' and is filled with small print. The backglass is a detailed artist's impression of the mountain peak at sunset, casting an impossibly long shadow across the clouds toward the horizon. The playfield has an open central section with an illustration of a woman juggling golden apples at the center. Two ramps rise from the playfield and cross paths in a figure 8. A set of bumpers labeled '+1' are in the upper right. In the upper left, a curved metallic shape hovers over the words 'Forcing Magnet'. Along the far left edge is a narrow gap next to the words 'AoC Lock'. Other toys and mechanisms are hidden while inactive. Above the playfield is a silvery dot-matrix display showing the current high score: ", self.hiscore;
	],
	before [;
		play: 
			if(self.ingame == 1)
				"You are already in the middle of a game!";
			self.ingame = 1;
			self.balls = 3;
			self.points = 0;
			objectloop(x has shootable) x.hitcount = 0;
			objectloop(x has shootable) x.newgame();
			self.gamenumber++;
			switch(self.gamenumber) {
			1: "You hit the start button and a melodious female voice greets you. ~Thank you for coming to see me, and welcome, learner! Are you ready to continue following the Path? Just SHOOT whatever target you like. The bumpers are a good place to start.~";
			2: "~I'm glad you're trying again. Don't get discouraged, your skill is increasing with every shot. What's hard now will be easy later. Of course, I'll have some new challenges for you then!~";
			3: "~We're starting to get to know each other well now, aren't we? I'd introduce myself, but I really have too many different names to try to share them all.~";
			default: "~Sure, I'm ready to go again!~";
			};
	];

object	skill "player skills"
	with
	catch 10,
	shoot 20,
	tilt 10,
	nudge 5,
	launch 25,
	multi 20,
	grit 15,
	luck 0;
