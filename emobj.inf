!============================================================================
! The room decorations

Target	lramp "left ramp" master_chamber
	with
	name 'left' 'ramp',
	triggers [;
		if((choicelock.lockready == 0) && (rramp.hitcount > 0)){
			choicelock.lockready = 1;
			"Infinity ramp has activated Axiom of Choice lock.";
		}
	];

Target	rramp "right ramp" master_chamber
	with
	name 'right' 'ramp',
	triggers [;
		if((choicelock.lockready == 0) && (lramp.hitcount > 0)){
			choicelock.lockready = 1;
			"Infinity ramp has activated Axiom of Choice lock.";
		}
	];

Target	magnet "forcing magnet" master_chamber
	with
	name 'forcing' 'magnet',
	value 50,
	risk 65,
	difficulty 125;

Target	bumpers "bumpers" master_chamber
	with
	name 'bumper' 'bumpers' 'successor',
	value 2,
	risk 40,
	difficulty 75,
	bounces 0,
	goal 50,
	newgame [;
		self.value = 2;
		self.goal = 50;
	],
	hit [;
		self.bounces = random(10);
		self.hitcount = self.hitcount + self.bounces;
		em.points = em.points + (self.bounces * self.value);
		print "The ball gets ", self.bounces, " hits in the successor bumpers for ", self.bounces * self.value, " points.^";
		if(self.hitcount < self.goal)
			print self.goal - self.hitcount, " more hits to reach Induction on N.^";
	],		
	triggers [;
		if(self.value == 2 && self.hitcount > 20){
			self.value = 4;
			"Since you like those bumpers so much, I'm doubling their value for you.";
		}
		if(self.value == 4 && self.hitcount > 49){
			self.value = 9;
			"Successor function reaches Induction on N. Opening Ordinal Staircase. (UNIMPLEMENTED)";
		}
	];

Target	choicelock "choice lock" master_chamber
	with
	name 'axiom' 'choice' 'lock',
	value 75,
	risk 75,
	difficulty 150,
	lockready 0,
	hasball 0,
	newgame [;
		self.lockready = 0;
		self.hasball = 0;
	],
	triggers [;
		if(self.lockready == 0)
			"You need to activate both sides of the Infinity ramp to prime the Axiom lock.";
		if(self.hasball == 1){
			self.hasball = 0;
			em.points = em.points + 500;
			"MULTIBALL! 500 point bonus.";
		}
		self.hasball++;
		"The ball hits the Axiom of Choice lock dead-on and the mechanism grabs it with a satisfying thunk.^";
	];

!============================================================================
! The immobile prop objects

Prop	em "Enlightened Master" master_chamber
	with
	article "the",
	name 'enlightened' 'master' 'pinball' 'machine' 'game' 'table' 'pin',
	points 0,
	hiscore 0,
	ingame 0,
	balls 0,
	description [;
		print_ret "The Enlightened Master is a pinball machine with a purple cabinet and gold metal. The standard flipper buttons are bright pink. A card in the corner of the machine is labelled 'INSTRUCTIONS' and is filled with small print. The backglass is a detailed artist's impression of the mountain peak at sunset, casting an impossibly long shadow across the clouds toward the horizon. The playfield has an open central section with an illustration of a woman juggling golden apples at the center. Two ramps rise from the playfield and cross paths in a figure 8. A set of bumpers labeled '+1' are in the upper right. On the left, a curved metallic shape hovers over the words 'Forcing Magnet'. Numerous other toys and redirections are arranged around the perimeter of the playfield. You can't see them clearly unless the machine is being played. Above the playfield is a silvery dot-matrix display showing the current high score: ", self.hiscore;
	],
	before [;
		play: 
			if(self.ingame == 1)
				"You are already in the middle of a game!";
			self.ingame = 1;
			self.balls = 3;
			self.points = 0;
			objectloop(x has shootable) x.hitcount = 0;
			objectloop(x has shootable) x.newgame();
			"You hit the start button and a melodious female voice greets you. 'Welcome, learner, are you ready to walk the Path?'";
	];

object	skill "player skills"
	with
	catch 10,
	shoot 20,
	tilt 10,
	nudge 5,
	launch 25,
	multi 20,
	grit 15,
	luck 10;
